/*----------------------------------------------------------------------------*/
/* r4f_linker.cmd                                                                 */
/*                                                                            */
/* (c) Texas Instruments 2016, All rights reserved.                           */
/*                                                                            */

/* USER CODE BEGIN (0) */
/* USER CODE END */


/*----------------------------------------------------------------------------*/
/* Linker Settings                                                            */
--retain="*(.intvecs)"

/*----------------------------------------------------------------------------*/
/* Memory Map                                                                 */
MEMORY{
PAGE 0:
    VECTORS  (X)  : origin=0x00000000 length=0x00000100
    TCMA_RAM (RX) : origin=0x00000100 length=0x00007F00
    TCMB_RAM (RW) : origin=0x00080000 length=0x00008000
    L2_RAM (RW)   : origin=0x10200000 length=0x000F0000
    L3_RAM (RW)   : origin=0x88000000 length=0x00390000
    HWA_RAM (RW)  : origin=0x82000000 length=0x00020000

PAGE 1:
    L3_RAM (RW)   : origin=0x88000000 length=0x00390000
}

/*----------------------------------------------------------------------------*/
/* Section Configuration                                                      */
SECTIONS{
    .intvecs : {} > VECTORS
    
    /* The linker notation "X >> Y | Z" indicates section X is first allocated in Y
       and allowed to overflow into Z and can be split from Y to Z.
       The linker notation "X > Y | Z" indicates section X is first allocated in Y
       and allowed to overflow into Z and cannot be split from Y to Z. Some sections
       like bss are not allowed to be split so > notation is used for them 
    */
    .text    : {} >> TCMA_RAM | L2_RAM
    
    .const   : {} > L2_RAM
    .switch  : {} > L2_RAM
    .cio:    : {} > L2_RAM
    .data:   : {} > L2_RAM
    
    .cinit   : {} > L2_RAM
    .pinit   : {} > L2_RAM
    .bss     : {} > L2_RAM
    .stack   : {} > TCMB_RAM | L2_RAM

    .boot:{
       *.*(*ti_sysbios_family_arm_MPU*)
       boot.aer5f*(*.text)
       *.*(*startup*)
       *.*(*Startup*)
       *.*(*Cache*)
     } > TCMA_RAM | TCMB_RAM
    .l3ram   : {} > L3_RAM
}
/*----------------------------------------------------------------------------*/

